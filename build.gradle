// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    google()
    mavenCentral()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$Config.gradleVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$Config.kotlinVersion"
    classpath "org.jlleitschuh.gradle:ktlint-gradle:$Config.gradleVersion"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id "org.jlleitschuh.gradle.ktlint" version "10.2.0"
}

subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"

  ktlint {
    debug = false
  }

  afterEvaluate { project ->
    if (project.plugins.findPlugin("android") ?: project.plugins.findPlugin("android-library")) {
      android {
        compileSdkVersion Config.compileSdk
        buildToolsVersion Config.buildTools

        defaultConfig {
          minSdkVersion Config.minSdk
          targetSdkVersion Config.targetSdk
          versionCode Releases.versionCode
          versionName Releases.versionName
          multiDexEnabled true

          testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
          sourceCompatibility Config.javaVersion
          targetCompatibility Config.javaVersion
        }

        buildTypes {
          release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
          }
        }

        buildFeatures {
          viewBinding true
        }

        kotlinOptions {
          jvmTarget = Config.javaVersion
        }
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
